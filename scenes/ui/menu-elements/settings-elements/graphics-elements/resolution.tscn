[gd_scene load_steps=2 format=3 uid="uid://cn8r63dmvd55c"]

[sub_resource type="GDScript" id="GDScript_utf5l"]
resource_name = "resolution"
script/source = "extends HBoxContainer

# Default values for the element
@export_enum(
	\"3840x2160\", 
	\"2560x1440\", 
	\"1920x1080\", 
	\"1280x720\", 
	\"960x540\"
) var defaultValue: String = \"1920x1080\"

# Toggle window resizing by the user
@export var resizable: bool = false
# Toggle based on whether the element handles a setting that requires an in game node to exist
@export var isInGameSetting: bool = false
# Toggle based on whether the element is a sub element or not
@export var isSubElement: bool = false

# Reference to the section the settings element is under
@onready var sectionRef: TabBar = owner
# The section the settings element is under
@onready var section: StringName = sectionRef.name
# The name of the settings element
@onready var element: StringName = name

# Element node references
@onready var optionsRef: OptionButton = $Options

# Resource for common functions between settings elements
const ElementResource: Resource = SettingsDataManager.ElementResource

# List of options related to the settings element
const OPTION_LIST: Dictionary = {
	\"3840x2160\": Vector2i(3840, 2160),
	\"2560x1440\": Vector2i(2560, 1440),
	\"1920x1080\": Vector2i(1920, 1080),
	\"1280x720\": Vector2i(1280, 720),
	\"960x540\": Vector2i(960, 540)
}

# Index of the currently selected item
var selectedIndex: int
# The current value of the element
var currentValue: String


func _ready():
	# Initialize the element
	ElementResource.init_element(&\"option\", sectionRef, self)
	
	# Toggle window resizing
	get_viewport().get_window().set_flag(Window.FLAG_RESIZE_DISABLED, !resizable)


# Loads the saved/default values of the element
func load_settings() -> void:
	# List of values for the element
	var VALUES: Dictionary = {
		\"defaultValue\": defaultValue,
		\"validOptions\": OPTION_LIST
	}
	
	# Get the current value for the element
	currentValue = ElementResource.load_element_settings(VALUES, sectionRef, self)
	# Initialize the option button
	ElementResource.init_option_button_element(OPTION_LIST, optionsRef, currentValue, self)


func option_selected(index: int) -> void:
	# Update the settings cache
	ElementResource.option_selected(sectionRef, self, index)


# Called to apply the settings in the settings cache
func apply_settings() -> void:
	# Do nothing if the game is in fullscreen mode
	if DisplayServer.window_get_mode() == DisplayServer.WINDOW_MODE_EXCLUSIVE_FULLSCREEN:
		return
	
	# Do nothing if the game is in borderless mode
	if DisplayServer.window_get_flag(DisplayServer.WINDOW_FLAG_BORDERLESS):
		return
	
	# Change the window size to the selected resolution
	DisplayServer.window_set_size(OPTION_LIST[currentValue])
	# Change the viewport size to the selected resolution for correct scaling
	get_viewport().set_size(OPTION_LIST[currentValue])
	# Center the game window
	get_window().move_to_center()
"

[node name="Resolution" type="HBoxContainer"]
anchors_preset = 15
anchor_right = 1.0
anchor_bottom = 1.0
grow_horizontal = 2
grow_vertical = 2
script = SubResource("GDScript_utf5l")

[node name="Label" type="Label" parent="."]
layout_mode = 2
size_flags_horizontal = 3
size_flags_vertical = 1
text = "Resolution"
vertical_alignment = 1

[node name="Options" type="OptionButton" parent="."]
layout_mode = 2
size_flags_horizontal = 3
focus_mode = 0
